//file:noinspection all

plugins {
	id 'fabric-loom' version "0.10-SNAPSHOT"
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
group = project.maven_group

version = System.env.GITHUB_ACTIONS == "true"
		? "${project.mod_version}+build.${System.env.GITHUB_RUN_NUMBER}"
		: project.mod_version

static String terraformers() {
	def url = "https://maven.terraformersmc.com/"
	def conn = new URL(url).openConnection()
	conn.connect()
	url = conn.getResponseCode() == 521 ? "https://maven.kotlindiscord.com/repository/terraformers/" : url
	return url
}

repositories {
	maven {
		name = "Modrinth"
		url  = "https://api.modrinth.com/maven/"
	}
	maven {
		name = 'Shedaniel Maven'
		url "https://maven.shedaniel.me/"
	}
	maven {
		name = 'Curse Maven'
		url = "https://www.cursemaven.com"
	}
	maven {
		url = terraformers()
	}
	maven { url = "https://maven.devos.community/releases/" }
	maven { url = "https://maven.devos.community/snapshots/" }
	mavenCentral()
}

dependencies {
	// Required //
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Api's //
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}"

	modImplementation(include("me.alphamode:ForgeTags:${project.forge_tags_version}"))

	modImplementation(include("curse.maven:forge-config-api-port-fabric-547434:${project.config_api_version}"))
	implementation 'com.electronwill.night-config:core:3.6.4'
	implementation 'com.electronwill.night-config:toml:3.6.4'

	// Runtime Utilites //
//	modRuntimeOnly "maven.modrinth:sodium:${project.sodium_version}"
	modRuntimeOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

sourceSets.main.resources {
	srcDirs += "$projectDir/src/generated/resources"
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

jar {
	from("LICENSE" && "LICENSE_ASSETS") {
		rename { "${it}_${project.archives_base_name}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {}
}